
$ontext

Global Negishi welfare is a weighted sum of the discounted logarithm of

macroeconomic consumption, adjusted for the economic losses generated by non-

market damages.  The parameter catt defines the region-specific value of

the catastrophic level of temperature.  The parameter hsx modifies the

quadratic loss function so as to allow for a hockey-stick shape.



In the Pareto-optimal case, nonmarket damages enter the Negishi welfare

definition, and market damages represent one of the competing claims on the

allocation of total production resources.  In all other cases, nonmarket

and market damages are excluded from the determination of the equilibrium,

but are determined in a post-optimization step.  In this way, we allow for

the possibility of non-cooperation or of non-optimal international control

agreements.

$offtext


* ============================================================================= *
* ============================ EQUATIONS DEFINITION =========================== *
* ============================================================================= *

* ------------------------------------------------------------------------------*
*  This section of the code contains the equation definitions.
*  (M5.VEQ + CSE.TAB + DAMAGES.TAB)
* ------------------------------------------------------------------------------*

LKK(rg,tp,sw)$lk(tp)..      K(rg,tp,"sw1")         =e=    K(rg,tp,sw);
LKKN(rg,tp,sw)$lk(tp)..     KN(rg,tp,"sw1")        =e=    KN(rg,tp,sw);
LKY(rg,tp,sw)$lk(tp)..      Y(rg,tp,"sw1")         =e=    Y(rg,tp,sw);
LKYN(rg,tp,sw)$lk(tp)..     YN(rg,tp,"sw1")        =e=    YN(rg,tp,sw);
LKE(rg,tp,sw)$lk(tp)..      E(rg,tp,"sw1")         =e=    E(rg,tp,sw);
LKEN(rg,tp,sw)$lk(tp)..     EN(rg,tp,"sw1")        =e=    EN(rg,tp,sw);
LKN(rg,tp,sw)$lk(tp)..      N(rg,tp,"sw1")         =e=    N(rg,tp,sw);
LKNN(rg,tp,sw)$lk(tp)..     NN(rg,tp,"sw1")        =e=    NN(rg,tp,sw);
LKPE(et,tp,ts,rg,sw)$lk(tp)..  PE(et,tp,ts,rg,"sw1")  =e=    PE(et,tp,ts,rg,sw);
LKCPE(tp,sw)$lk(tp)..       CPE(tp,"sw1")          =e=    CPE(tp,sw);
LKCPN(tp,sw)$lk(tp)..       CPN(tp,"sw1")          =e=    CPN(tp,sw);
LKPN(nt,tp,rg,sw)$lk(tp)..  PN(nt,tp,rg,"sw1")     =e=    PN(nt,tp,rg,sw);
LKPNF(ura,tp,ts,rg,sw)$lk(tp)..  PNF(ura,tp,rg,"sw1")     =e=    PNF(ura,tp,rg,sw);
LKGASNON(tp,rg,sw)$lk(tp).. GASNON(tp,rg,"sw1")    =e=    GASNON(tp,rg,sw);
LKOILNON(tp,rg,sw)$lk(tp).. OILNON(tp,rg,"sw1")    =e=    OILNON(tp,rg,sw);
LKURSC(rg,x,tp,sw)$lk(tp).. URSC(rg,x,tp,"sw1")    =e=    URSC(rg,x,tp,sw);
LKURSCURA(rg,ura,tp,sw)$lk(tp).. URSCURA(rg,ura,tp,"sw1") =e= URSCURA(rg,ura,tp,sw);
LKPRSV(rg,x,tp,sw)$lk(tp).. PRSV(rg,x,tp,"sw1")    =e=    PRSV(rg,x,tp,sw);
LKPRSVURA(rg,ura,tp,sw)$lk(tp).. PRSVURA(rg,ura,tp,"sw1") =e= PRSVURA(rg,ura,tp,sw);
LKRA(rg,x,tp,sw)$lk(tp)..   RA(rg,x,tp,"sw1")      =e=    RA(rg,x,tp,sw);
LKRAURA(rg,ura,tp,sw)$lk(tp).. RAURA(rg,ura,tp,"sw1") =e= RAURA(rg,ura,tp,sw);
LKEISS(tp,rg,sw)$lk(tp)..   EISS(tp,rg,"sw1")      =e=    EISS(tp,rg,sw);
LKC(rg,tp,sw)$lk(tp)..      C(rg,tp,"sw1")         =e=    C(rg,tp,sw);
LKI(rg,tp,sw)$lk(tp)..      I(rg,tp,"sw1")         =e=    I(rg,tp,sw);
LKCLEV(tp,rg,sw)$lk(tp)..   CLEV(tp,rg,"sw1")      =e=    CLEV(tp,rg,sw);
LKMLEV(tp,rg,sw)$lk(tp)..   MLEV(tp,rg,"sw1")      =e=    MLEV(tp,rg,sw);
LKOLEV(o2g,tp,rg,sw)$lk(tp)..
                            OLEV(o2g,tp,rg,"sw1")  =e=    OLEV(o2g,tp,rg,sw);
LKDC(rg,tp,sw)$lk(tp)..     DC(rg,tp,"sw1")        =e=    DC(rg,tp,sw);
LKCRLX(tp,rg,sw)$lk(tp)..   CRLX(tp,rg,"sw1")      =e=    CRLX(tp,rg,sw);
LKABATE(ghg,abx,tp,rg,sw)$lk(tp)..
                        ABATE(ghg,abx,tp,rg,"sw1") =e= ABATE(ghg,abx,tp,rg,sw);
LKAFF(sy,ps,sw)$lk(sy)..    AFF(sy,ps,"sw1")       =e=    AFF(sy,ps,sw);
LKEXPRT(trd,tp,rg,sw)$lk(tp)..
                            EXPRT(trd,tp,rg,"sw1")  =e=   EXPRT(trd,tp,rg,sw);
LKATP(tp,sw)$lk(tp)..       ATP(tp,"sw1")           =e=   ATP(tp,sw);
LKMD(rg,tp,sw)$lk(tp)..     MD(rg,tp,"sw1")         =e=   MD(rg,tp,sw);
LKEC(rg,tp,sw)$lk(tp)..     EC(rg,tp,"sw1")         =e=   EC(rg,tp,sw);
LKNTX(trd,tp,rg,sw)$lk(tp)..
                            NTX(trd,tp,rg,"sw1")    =e=   NTX(trd,tp,rg,sw);

LKEM(ghg,tp,sw)$lk(tp)..    EM(ghg,tp,"sw1")        =e=   EM(ghg,tp,sw);
LKTOTEM(ghg,tp,sw)$lk(tp).. TOTEM(ghg,tp,"sw1")     =e=   TOTEM(ghg,tp,sw);
LKTOTRGEM(tp,rg,sw)$lk(tp)..
                            TOTRGEM(tp,rg,"sw1")    =e=   TOTRGEM(tp,rg,sw);
LKCO2(box,tp,sw)$lk(tp)..   CO2(box,tp,"sw1")       =e=   CO2(box,tp,sw);
LKS(ghg,tp,sw)$lk(tp)..     S(ghg,tp,"sw1")         =e=   S(ghg,tp,sw);
LKSM(ghg,tp,sw)$lk(tp)..    SMINUS(ghg,tp,"sw1")    =e=   SMINUS(ghg,tp,sw);
LKPT(tp,sw)$lk(tp)..        PT(tp,"sw1")            =e=   PT(tp,sw);

$ontext

There is a putty-clay (nested CES) formulation of substitution between new
capital, labor, electric and nonelectric energy in the production of the
composite output good. In the case of old vintages, there is no substitution.
New capital at time tp+1 is a distributed lag function of the investments made
in year tp and tp+1.  This equation allows for varying the length of each period.
In most periods, nyper(tp) = 10.

$offtext
newcap(rg,tp+1,sw) $(tperly(tp) and st(tp+1,sw))..
                KN(rg,tp+1,sw)-.5*nyper(tp)*I(rg,tp+1,sw) =e=

               .5*nyper(tp) * speed(tp,rg) * I(rg,tp  ,sw);


newprod(rg,pp,sw)$st(pp,sw)..              YN(rg,pp,sw) =l=

 (aconst(pp,rg)*KN(rg,pp,sw)**(rho(rg)*kpvs(rg))

               *ln(pp,rg)**(rho(rg)*(1 - kpvs(rg)))

+ bconst(pp,rg)*EN(rg,pp,sw)**(rho(rg)*elvs(rg))

            *NN(rg,pp,sw)**(rho(rg)*(1 - elvs(rg))))**(1/rho(rg));


newelec(rg,tp+1,sw) $(tperly(tp) and st(tp+1,sw))..

                EN(rg,tp+1,sw) - E(rg,tp+1,sw) =e= - E(rg,tp,sw)*speed(tp,rg);


newnon(rg,tp+1,sw) $(tperly(tp) and st(tp+1,sw))..

                NN(rg,tp+1,sw) - N(rg,tp+1,sw) =e= - N(rg,tp,sw)*speed(tp,rg);


totalcap(rg,tp+1,sw) $ (tperly(tp) and st(tp+1,sw))..

                K(rg,tp+1,sw) - KN(rg,tp+1,sw)  =e= K(rg,tp,sw)*speed(tp,rg);


totalprod(rg,tp+1,sw) $(tperly(tp) and st(tp+1,sw))..

                Y(rg,tp+1,sw) - YN(rg,tp+1,sw)  =e= Y(rg,tp,sw)*speed(tp,rg);


tc(rg,tlast,sw)..

                      I(rg,tlast,sw) =g=

                     (K(rg,tlast,sw) * (grow(tlast,rg) + (1 - spda(rg))));



nweldf..    NWEL =E=

     100 * 1000 *( sum(rg, nwt(rg) *
                                    sum((sw,pp),

                                    prob(sw)*udf(pp,rg)

                                    *log(C(rg,pp,sw)))));
