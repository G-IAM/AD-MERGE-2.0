PARAMETERS
    I0(RG)        INITIAL INVESTMENT
    C0(RG)        INITIAL CONSUMPTION
    K0(RG)        INITIAL CAPITAL
    E0(RG)        INITIAL ELECTRIC ENERGY
    N0(RG)        INITIAL NON-ELECTRIC ENERGY
    Y0(RG)        INITIAL OUTPUT
    RHO(RG)       ESUB MINUS ONE DIVIDED BY ESUB
    ACONST(TP,RG) CONSTANT FOR CAPITAL-LABOR INDEX
    BCONST(TP,RG) CONSTANT FOR ELECTRIC-NON-ELECTRIC ENERGY INDEX
    WTP(RG,TP,SW) FRACTIONAL GDP LOSS FOR WTPTEMP AT ALL INCOMES
    HSX(RG,TP,SW) HOCKEY-STICK EXPONENT
    KGDP(RG)      INITIAL CAPITAL-GDP RATIO
    DEPR(RG)      ANNUAL PERCENT DEPRECIATION
    SPDA(RG)      SPEED OF ADJUSTMENT
    SPEED(TP,RG)  PERIOD ADJUSTMENT SPEED
    PNREF(RG)     REFERENCE PRICE OF NON-ELECTRIC ENERGY - DOLLARS PER GJ
    PEREF(RG)     REFERENCE PRICE OF ELECTRIC ENERGY - MILLS PER KWH
    ESUB(RG)      ELASTICITY BETWEEN K-L AND E-N
    KPVS(RG)      CAPITAL VALUE SHARE PARAMETER
    ELVS(RG)      ELECTRIC VALUE SHARE PARAMETER
    XNTAX(RG)     EXISTING TAX RATE ON NONELECTRIC ENERGY
    XETAX(RG)     EXISTING TAX RATE ON ELECTRIC ENERGY
    DECF(RG)      MAX DECLINE FACTOR (ANNUAL) FOR PE-DL -  SYNF -  NE-BAK
    DECFO(RG)      MAX DECLINE FACTOR (ANNUAL) FOR Oil non-electric
    DECFH2(RG)      MAX DECLINE FACTOR (ANNUAL) FOR H2
    NSHF(RG)      MAX MARKET SHARE FACTOR FOR EX AND NX TECHNOLOGIES
    EXPF(RG)      MAX EXPANSION FACTOR (ANNUAL) FOR XLE TECHNOLOGIES
    EXPNU(RG)      MAX EXPANSION FACTOR (ANNUAL) FOR Nuc 
    EXPFO(RG)      MAX EXPANSION FACTOR (ANNUAL) FOR Oil non-electric
    hxpf(RG)      MAX EXPANSION FACTOR (ANNUAL) FOR H2
    NXPF(RG)      MAX EXPANSION FACTOR (ANNUAL) FOR XLN TECHNOLOGIES
    HXPF(RG)      MAX EXPANSION FACTOR (ANNUAL) FOR H2 TECHNOLOGIES
    OGPD(RG)      OIL-GAS PRICE DIFFERENTIAL - DOLLARS PER GJ
    CLGDP(RG)     COAL-GNP GROWTH ELASTICITY
    DFACTCURR(TP,RG)   CURRENT ANNUAL UTILITY DISCOUNT FACTOR
    UDF(TP,RG)         UTILITY DISCOUNT FACTOR FOR PERIOD TP
    UDFSUM(RG)         UDF NORMALIZING CONSTANT
    L(TP,RG)           CURRENT LABOR FORCE (EFFICIENCY UNITS)
    LN(TP,RG)          LABOR FORCE - NEW VINTAGE
    KL(TP,RG)          CAPITAL-LABOR INDEX
    YREF(TP,RG)        REFERENCE VALUE OF GROSS OUTPUT
    KREF(TP,RG)        REFERENCE VALUE OF CAPITAL
    YCHK(TP,RG)        CHECK VALUE OF GROSS OUTPUT
    EREF(TP,RG)        REFERENCE QUANTITY OF ELECTRICITY - TKWH
    NREF(TP,RG)        REFERENCE QUANTITY OF NONELECTRIC ENERGY
    AEEIFAC(TP,RG)     AEEI FACTOR
    AEEIFAC2(TP,RG)     AEEI FACTOR for non-electric techs
    PVPI(TRD,TP,SW)        PRESENT VALUE PRICES OF TRADEABLES
    FTPI(SW,TRD,TP)        FUTURE VALUE PRICES OF TRADEABLES
    NWT(RG)                NEGISHI WEIGHTS
    elgfrac(RG)              Gas fraction of electricity
    NWTITR(ITER,RG)        NEGISHI WEIGHTS AT EACH ITERATION
    TAXREV(RG,TP,SW)       ENERGY TAX REVENUES FOR EACH REGION
    TAXITR(RG,TP,ITER,SW)  TAX REVENUES AT EACH ITERATION;


WTP(RG,TP,SW) =
            WTPLOSS(SW)/ ( 1 + 100*EXP(-0.1842*PCGDP(TP,RG)));
HSX(RG,TP,SW)$(CATT(RG,SW) LT INF) = LOG(1-  WTP(RG,TP,SW))/
                                     LOG(1- (WTPTEMP(SW)/CATT(RG,SW))**2);
* UPPER BOUND ON HOCKEY STICK EXPONENTIAL TO AVOID POTENTIAL NON-CONVEXITIES
HSX(RG,TP,SW) = MIN(HSX(RG,TP,SW),1.000);

DISPLAY CATT, WTP, HSX;
NWT(RG)   = POTGDP("2015",RG) / SUM(R, POTGDP("2015",R));
NWT(RG)   = MACRO("NW",RG);
elgfrac(RG)= 0.6;
elgfrac('Africa')=0.9;

DISPLAY NWT;

NWTITR("IT0",RG) = NWT(RG);

DISPLAY NWTITR;

KGDP(RG)  = MACRO("KGDP", RG);
DEPR(RG)  = MACRO("DEPR", RG);
PNREF(RG) = MACRO("PNREF", RG);
ESUB(RG)  = MACRO("ESUB", RG);
KPVS(RG)  = MACRO("KPVS", RG);
ELVS(RG)  = MACRO("ELVS", RG);
XNTAX(RG) = MACRO("XNTAX", RG);
XETAX(RG) = MACRO("XETAX", RG);
DECF(RG)  = MACRO("DECF", RG);
DECFO(RG) =MACRO("DECFO", RG);
DECFH2(RG)=MACRO("DECFH2", RG);
NSHF(RG)  = MACRO("NSHF",RG);
EXPF(RG)  = MACRO("EXPF", RG);
EXPNU(RG) = MACRO("EXPNU", RG);

HXPF(RG)  = MACRO("HXPF", RG);
EXPFO(RG)  = MACRO("EXPFO", RG);
hxpf(RG)  = MACRO("hxpf", RG);
NXPF(RG)  = MACRO("NXPF", RG);
CLGDP(RG) = MACRO("CLGDP", RG);
OGPD(RG)  = MACRO("OGPD", RG);

*   +15
AEEIFAC("2015",RG) = 1;
LOOP(TP,
     AEEIFAC(TP+1,RG) = ( (1 - AEEI(TP,RG)) ** NYPER(TP) ) * AEEIFAC(TP,RG)
    );
    
AEEIFAC2(TP,RG)=AEEIFAC(TP,RG);
AEEIFAC2("2015","africa")=1;

LOOP(TP,
     AEEIFAC2(TP+1,"africa") = ((1 - (1.75*GROW(tp,"africa")-PGRO(tp,"africa"))) ** NYPER(TP) ) * AEEIFAC2(TP,"africa")
    );
DISPLAY AEEIFAC,AEEIFAC2;
DFACTCURR(TP,RG)  =  1 - MPC(TP) + GROW(TP,RG);
K0(RG)  =  KGDP(RG)*POTGDP("2015",RG);
SPDA(RG)      =  1 - DEPR(RG)/100;
SPEED(TP,RG)  =  SPDA(RG)**NYPER(TP);
RHO(RG) = (ESUB(RG) - 1)/ESUB(RG);
TAXREV(RG,TP,SW) = 0;
LOOP(TBASE,
     E0(RG) = elgen("totelec",rg);
     N0(RG) = SUM(NT, NCAP(NT,TBASE,RG))
              - NCAP("COAL-1",TBASE, RG)
              - HTRT("OIL-R",TBASE,RG)*ECAP("OIL-R",TBASE,RG)
              - HTRT("GAS-R",TBASE,RG)*ECAP("GAS-R",TBASE,RG)
              - HTRT("GAS-N",TBASE,RG)*ECAP("GAS-N",TBASE,RG)
              - HTRT("OIL-R",TBASE,RG)*ECAPPEAK("OIL-R",TBASE,RG)
              - HTRT("GAS-R",TBASE,RG)*ECAPPEAK("GAS-R",TBASE,RG)
              - HTRT("GAS-N",TBASE,RG)*ECAPPEAK("GAS-N",TBASE,RG)
              - OILX(TBASE,RG) - GASX(TBASE,RG);
     I0(RG) = POTGDP(TBASE,RG) * KGDP(RG) * (GROW(TBASE,RG) + (DEPR(RG)/100));
     C0(RG) = POTGDP(TBASE,RG) - I0(RG) ;
     Y0(RG) = I0(RG) + C0(RG)
            + ( SUM(ET, ECAP(ET,TBASE,RG)*ECST(ET,TBASE,RG))
                - OILX(TBASE,RG) *  MACRO("INTPR",RG)
                - GASX(TBASE,RG) *  MACRO("INTPR",RG)
                + SUM(NT, NCAP(NT,TBASE,RG)*NCST(NT,TBASE,RG))
              )/1000;
    );
PEREF(RG) =     ELVS(RG) * PNREF(RG) * N0(RG)/((1-ELVS(RG))*E0(RG));
DISPLAY Y0, PEREF, RHO, ESUB;
LOOP(TP,NCAP("CLDU",TP+1,RG) =
            NCAP("CLDU",TP,RG)*(( 1 + (GROW(TP,RG)*CLGDP(RG)) )**NYPER(TP))
    );
            NCAP("CLDU","2020","china")  = 58.5;
        NCAP("CLDU","2020","India")  = 12.72;
        NCAP("CLDU","2020","USA")  = NCAP("CLDU","2020","USA");
LOOP(tp10,NCAP("CLDU",tp10+1,"India") =
            NCAP("CLDU",tp10,"India")*(( 1 + (GROW(tp10,"India")*CLGDP("India")) )**NYPER(tp10))
    );
LOOP(tp10,NCAP("CLDU",tp10+1,"china") =
            NCAP("CLDU",tp10,"china")*(( 1 + (GROW(tp10,"china")*CLGDP("china")) )**NYPER(tp10))
    );  
        NCAP("CLDU",TP90,RG)  = NCAP("CLDU","2100",RG);

        

DISPLAY ECAP, NCAP;
UDF(TBASE,RG)  =  1;
L(TBASE,RG)    =  1;
LOOP(TP,
      L(TP+1,RG)    =  L(TP,RG)*(1 + GROW(TP,RG))**NYPER(TP) ;
      LN(TP+1,RG)   =  L(TP+1,RG) - L(TP,RG)*SPDA(RG)**NYPER(TP) ;
      UDF(TP+1,RG)  =  UDF(TP,RG)*DFACTCURR(TP,RG)**NYPER(TP)
    );
      KREF(TP,RG)   =  L(TP,RG) * K0(RG);
      EREF(TP,RG)   =  L(TP,RG) * E0(RG) * AEEIFAC(TP,RG);
      NREF(TP,RG)   =  L(TP,RG) * N0(RG) * AEEIFAC2(TP,RG);
display L, POTGDP, PEREF, EREF, PNREF, NREF;
      YREF(TP,RG)   =  L(TP,RG) * POTGDP("2015",RG)
                                    + (PEREF(RG)*EREF(TP,RG)
                                    +  PNREF(RG)*NREF(TP,RG))/1000;
DISPLAY E0, N0, EREF, NREF, YREF;

BCONST(TP,RG)   = (PNREF(RG)/1000)*YREF(TP,RG)**(RHO(RG) - 1) /
                        ((1 - ELVS(RG))*(EREF(TP,RG)**(RHO(RG)*ELVS(RG)))
                        *(NREF(TP,RG)**(RHO(RG)*(1 - ELVS(RG)) - 1)));
ACONST(TP,RG)  =    (EREF(TP,RG)**ELVS(RG)) * (NREF(TP,RG)**(1-ELVS(RG)));
ACONST(TP,RG)  =    YREF(TP,RG)**RHO(RG)
                  - BCONST(TP,RG)*ACONST(TP,RG)**RHO(RG);
KL(TP,RG) = KREF(TP,RG)**(rho(rg)*kpvs(rg))*L(tp,rg)**(rho(rg)*(1 - kpvs(rg)));
ACONST(TP,RG)   = ACONST(TP,RG)/KL(TP,RG);
YCHK(TP,RG)   = (EREF(TP,RG)**ELVS(RG)) * (NREF(TP,RG)**(1-ELVS(RG)));
YCHK(TP,RG)   = BCONST(TP,RG) *  YCHK(TP,RG)**RHO(RG);
YCHK(TP,RG)   = ACONST(TP,RG)*KL(TP,RG) + YCHK(TP,RG);
YCHK(TP,RG)  =  YCHK(TP,RG)**(1/RHO(RG));

OPTION DECIMALS = 6;
DISPLAY KL, ACONST, BCONST, YREF, YCHK;

EISDEMAND(PP,RG)  =  EISDEMAND("2015",RG) *  L(PP,RG)**EISELAS;
EISDEMAND(PP, "WORLD") = SUM(R, EISDEMAND(PP,R));
DISPLAY EISDEMAND;

UDF(TLAST,RG) = UDF(TLAST,RG) * ARBM;
OPTION DECIMALS = 3;
DISPLAY UDF;

UDFSUM(RG)     =  SUM(PP, UDF(PP,RG));
UDF(TBASE,RG)  =  0.0;
UDF(PP,RG)     =  UDF(PP,RG)/UDFSUM(RG);

DISPLAY "UDF VALUES - AFTER NORMALIZATION AND CORRECTION FOR PERIOD LENGTH";

DISPLAY UDF, L;